/*******************************************************************************
 * <copyright> Copyright (c) 2009-2017 Bauhaus Luftfahrt e.V.. All rights reserved. This program and the accompanying
 * materials are made available under the terms of the Eclipse Public License v1.0 which accompanies this distribution,
 * and is available at http://www.eclipse.org/legal/epl-v10.html </copyright>
 ******************************************************************************/

import java.util.List
import oida.ontologyMgr.Library
import oida.ontologyMgr.OntologyMetaInfo
import oida.util.OIDAUtil
import oida.ontologyMgr.SystemOntologyMetaInfo
import oida.ontologyMgr.LocalOntologyMetaInfo
import oida.util.constants.StringConstants
import oida.ontology.ui.view.OntologyLibraryView

/**
 * 
 * @since 2016-11-08
 * @author Michael Shamiyeh
 * 
 */
module oida.ontology.ui.OntologyLibraryView {
	parts {
		viewpart oida.ontology.ui.OntologyLibraryView {
			viewname "Ontology Library"
			viewclass OntologyLibraryView
		}
	}

	featuresProvider {
		features {
			OntologyMetaInfo -> iri, nrOfClasses, nrOfIndividuals, nrOfObjectProperties
		}
	}

	bindings {
		value List<Integer> TableColumnWeights -> #[50, 200, 25, 25, 25]
	}
	
	viewerContentProvider {
		elements{
			Library -> ontologies
		}
		children{
			Library -> ontologies
		}
	}
	
	labelProvider {
		text {
			Library -> 'Library'
			OntologyMetaInfo -> iri
			LocalOntologyMetaInfo -> OIDAUtil.extractFileName(localPath)
		}
		
		image {
			Library -> "OntologyLibrary_16.png"
			OntologyMetaInfo -> "OntologyLocal_16.png"
			SystemOntologyMetaInfo -> "OntologyRepository_16.png"
		}
	}

	tableLabelProvider {
		text {
			OntologyMetaInfo:iri -> iri.replaceAll("%20", StringConstants.SPACE)
			OntologyMetaInfo:nrOfClasses -> nrOfClasses.toString
			OntologyMetaInfo:nrOfIndividuals -> nrOfIndividuals.toString
			OntologyMetaInfo:nrOfObjectProperties -> nrOfObjectProperties.toString
		}
	}

	featureCaptionProvider {
		text {
			OntologyMetaInfo : iri -> 'IRI'
			OntologyMetaInfo : nrOfClasses -> 'Classes'
			OntologyMetaInfo : nrOfIndividuals -> 'Individuals'
			OntologyMetaInfo : nrOfObjectProperties -> 'Object Properties'
		}
	}
}
